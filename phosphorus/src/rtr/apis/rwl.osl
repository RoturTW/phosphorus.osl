
class RTRDocument extends rtr.value.RTRValue (
  def init(object doc) (
    self.doc @= doc
  )

  def getRtrType() (
    return rtr.value.RTRTypeValue({
      name: "document"
    })
  )
  
  def getItem(key) (
    switch key.stringify() (
      // values
      case "title"
        return rtr.value.RTRStrValue(self.doc.title)
      case "icon"
        if self.doc.icon == null (
          return rtr.value.RTRNullValue()
        )
        return rtr.value.RTRStrValue(self.doc.icon)
      case "root"
        local elems @= self.doc.rwlInst.elements
        for i elems.len (
          local elem @= elems[i]
          if elem.name == "RWLRoot" (
            return rtr.apis.rwl.RTRElement(elem)
          )
        )
        return rtr.value.RTRNullValue()
      
      case "vars"
        return self.doc.vars
      
      // methods
      case "redirect"
        return rtr.value.RTROslFuncValue(def(args, _inst, doc) -> (
          void doc.doc.loadUrl(shared.url.Url(args[1].stringify()))
          void layouts.shared.state.updateTab()
        ), self)
      case "getElement"
        return rtr.value.RTROslFuncValue(def(args, _inst, doc) -> (
          local elem @= doc.doc.rwlInst.getElement(args[1].stringify())
          if elem == null (
            return rtr.value.RTRNullValue()
          )
          return rtr.apis.rwl.RTRElement(elem)
        ), self)
      case "createTextElement"
        return rtr.value.RTROslFuncValue(def(args, _inst) -> (
          return rtr.apis.rwl.RTRElement(rwl.main.RWLElement(rtr.apis.rwl.rtrValueToRwlValue(args[1]), { flags: [], pairs: [] }))
        ))
      case "createIconElement"
        return rtr.value.RTROslFuncValue(def(args, _inst) -> (
          return rtr.apis.rwl.RTRElement(rwl.main.RWLIcon({ flags: [], pairs: [ ["src", rwl.value.RWLStrValue(args[1].stringify())] ] }))
        ))
      case "createScript"
        return rtr.value.RTROslFuncValue(def(args, _inst, doc) -> (
          local elem @= rwl.main.RWLScript(args[1].stringify(), { flags: [], pairs: [] }, doc.doc.rwlInst)
          void rtr.apis.main.addToMod(doc.doc, elem.mod)
          void elem.start()
          return rtr.apis.rwl.RTRElement(elem)
        ), self)
      case "createContainer"
        return rtr.value.RTROslFuncValue(def(args, _inst) -> (
          local kind = args[1].stringify()
          switch kind (
            case "frame"
              return rtr.apis.rwl.RTRElement(rwl.main.RWLFrame([], { flags: [], pairs: [] }))
            case "section"
              return rtr.apis.rwl.RTRElement(rwl.main.RWLSection([], { flags: [], pairs: [] }))
            case "button"
              return rtr.apis.rwl.RTRElement(rwl.main.RWLButton([], { flags: [], pairs: [] }))
            
            default
              throw "unknown container type" + kind
          )
          return rtr.apis.rwl.RTRElement(rwl.main.RWLElement(rtr.apis.rwl.rtrValueToRwlValue(args[1]), { flags: [], pairs: [] }))
        ))
      
      default
        throw "unknown property" + key.stringify() + "on document"
    )
  )
  
  def setItem(key, value) (
    switch key.stringify() (
      case "title"
        self.doc.title = value.stringify()
        break
      case "icon"
        self.doc.icon = value.stringify()
        break
      default
        throw "cannot set property" + key.stringify() + "on document"
    )
  )
)

class RTRElement extends rtr.value.RTRValue (
  isRwlElem = true
  
  def init(object elem) (
    self.elem @= elem
  )

  def getRtrType() (
    return rtr.value.RTRTypeValue({
      name: "element"
    })
  )
  
  def stringify() (
    local txt = (self.elem.blockName ?? self.elem.kind).toStr()
    if self.elem.id != null (
      txt ++= ":" ++ self.elem.id
    )
    return "<" ++ txt ++ ">"
  )
  
  def getItem(key) (
    local k = key.stringify()
    
    switch k (
      case "toggleFlag"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          local flags @= elem.elem.header.flags
          local name = args[1].stringify()
          
          if flags.contains(name) (
            void flags.delete(flags.index(name))
          ) else (
            void flags.append(name)
          )
          
          elem.elem.needsUpdate = true
          void elem.elem.checkUpdate()
        ), self)
      case "addFlag"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          local flags @= elem.elem.header.flags
          local name = args[1].stringify()
          
          if !flags.contains(name) (
            void flags.append(name)
          )
          
          elem.elem.needsUpdate = true
          void elem.elem.checkUpdate()
        ), self)
      case "removeFlag"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          local flags @= elem.elem.header.flags
          local name = args[1].stringify()
          
          if flags.contains(name) (
            void flags.delete(flags.index(name))
          )
          
          elem.elem.needsUpdate = true
          void elem.elem.checkUpdate()
        ), self)
      case "hasFlag"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          local flags @= elem.elem.header.flags
          local name = args[1].stringify()
          
          return rtr.value.RTRBoolValue(flags.contains(name))
        ), self)
      
      case "addChild"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          if elem.elem.children == null (
            throw elem.kind + "cannot contain children"
          )
          if args[1].isRwlElem != true (
            throw "argument is not rwl element"
          )
          void elem.elem.children.append(args[1].elem)
          
          void elem.elem.updateParentFromCache()
        ), self)
      case "popChild"
        return rtr.value.RTROslFuncValue(def(args, _inst, elem) -> (
          if elem.elem.children == null (
            throw elem.kind + "doesnt contain children"
          )
          local idx = args.len > 0 ? args[1].numbify() 1
          local out @= elem.elem.children[idx]
          if out != null (
            out @= rtr.apis.rwl.RTRElement(out)
          ) else (
            out @= rtr.value.RTRNullValue()
          )
          void elem.elem.children.delete(idx)
          
          void elem.elem.updateParentFromCache()
          return out
        ), self)
    )
    
    for i self.elem.header.pairs.len (
      local pair @= self.elem.header.pairs[i]
      if pair[1] == k (
        return rtr.apis.rwl.rwlValueToRtrValue(pair[2])
      )
    )
    return rtr.value.RTRNullValue()
  )
  
  def setItem(key, value) (
    local k = key.stringify()
    local v @= rtr.apis.rwl.rtrValueToRwlValue(value)
    
    if k == "text" and self.elem.name == "RWLElement" (
      self.elem.value @= v
      void self.elem.updateFromCache()
      return
    )
    
    if k == "src" and self.elem.name == "RWLIcon" (
      self.elem.src @= value.value
      void self.elem.updateFromCache()
      return
    )
    
    for i self.elem.header.pairs.len (
      local pair @= self.elem.header.pairs[i]
      if pair[1] == k (
        pair[2] @= v
        void self.elem.updateParentFromCache()
        return
      )
    )
    
    void self.elem.header.pairs.append([
      k, v
    ])
    
    void self.elem.updateParentFromCache()
  )
)

def rwlValueToRtrValue(object val) (
  switch val.type (
    case "str"
      return rtr.value.RTRStrValue(val.value)
    case "num"
      return rtr.value.RTRNumValue(val.value)
    case "percentage"
      return rtr.value.RTRPercentageValue(val.value)
    case "color"
      return rtr.value.RTRColorValue(val.value)
  )
  
  return rtr.value.RTRNullValue()
)

def rtrValueToRwlValue(object val) (
  local type @= val.getRtrType()
  
  switch type.data.name (
    case "str"
      return rwl.value.RWLStrValue(val.value)
    case "num"
      return rwl.value.RWLNumValue(val.value)
    case "percentage"
      return rwl.value.RWLPercentageValue(val.value)
    case "color"
      return rwl.value.RWLColorValue(val.value)
  )
  
  throw "cannot set" + type.stringify() + "as rwl property"
)

// applies rwl related apis to rtr
def addToMod(object doc, object mod) (
  void mod.declScopeVar("document", RTRDocument(doc))
  void mod.declScopeVar("theme", rtr.value.RTRObjValue({
    back: rtr.value.RTRColorValue(shared.theme.back),
    prim: rtr.value.RTRColorValue(shared.theme.prim),
    seco: rtr.value.RTRColorValue(shared.theme.seco),
    tert: rtr.value.RTRColorValue(shared.theme.tert),
    text: rtr.value.RTRColorValue(shared.theme.text),
    accent: rtr.value.RTRColorValue(shared.theme.accent),
  }))
)

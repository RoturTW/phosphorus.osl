
def url(object url) (
  if url.scheme == "local" (
    //local f @= open(url.domain_name).toStr()
    // fpp import instead (goes into output)
    local f @= import(url.domain_name).toStr()
    local r @= net.response.ValidResponse(url)
    r.fetch @= def() -> (
      self.content = self.f
      self.isFinished = true
    )
    r.f @= f
    return r
  )
  if url.scheme == shared.config.url.browser_scheme (
    local r @= net.response.ValidResponse(url)
    local page @= browserPages[url.domain_name]
    if typeof(page) == "string" (
      r.fetch @= def() -> (
        self.content = self.data
        self.isFinished = true
      )
      r.data @= page
    ) else (
      r.fetch @= page ?? (() -> ())
    )
    return r
  )

  local servers @= shared.config.net.servers
  local server @= servers[url.scheme]
  
  if server == null (
    local r @= net.response.NoResponse(url, "scheme '" ++ url.scheme ++ "' not found")
    return r
  )
  
  local resource = url.domain_top
  resource ++= "/"
  if url.domain_sub != null (
    resource ++= url.domain_sub
    resource ++= "."
  )
  resource ++= url.domain_name
  if url.resource != null (
    resource ++= "/"
    resource ++= url.resource
  )
  
  local tld = server.tlds[url.domain_top]
  
  if tld == null (
    local r @= net.response.NoResponse(url, "tld '" ++ url.domain_top ++ "' not found")
    return r
  )
  
  local realUrl = tld ++ "/" ++ resource
  
  local r @= net.response.ValidResponse(url)
  r.fetch @= def() -> (
    local o = self.realUrl.getAsync()
    if o != "Loading" and o != "404: Not Found" (
      self.content = o
      self.isFinished = true
    )
  )
  r.realUrl = realUrl
  
  return r
)


class Document (
  def init() (
    void self.createInsts()
    
    void self.createWorker()
  )
  
  def close() (
    void self.killWorker()
  )
  
  def createWorker() (
    //log "create worker"
    void killWorker()
    self.worker @= worker(shared.tab_worker)
    self.worker.document @= self
  )
  
  def killWorker() (
    if self.worker != null (
      //log "kill worker"
      void self.worker.kill()
      self.worker = null
    )
  )
  
  def createInsts() (
    self.title = "New Tab"
    if self.url != null (
      self.title = self.url.getTitle()
    )
    self.icon = null
    
    self.rtrInst @= rtr.main.RTR()
    self.rwlInst @= rwl.main.RWL(shared.document.empty, self.rtrInst)
  )
  
  def updateInsts() (
    // inject rwl apis
    void rtr.rwl.addToInst(self, self.rtrInst)
    
    // start onloads
    void self.rtrInst.startModules()
  )
  
  def update(array area) (
    void self.rwlInst.update(area)
  )
  
  def render(array area) (
    if self.worker != null (
      self.worker.area @= area
      if !self.worker.alive (
        self.rwlInst.errored = true
        self.rwlInst.errormsg = "tab worker died, check code for syntax errors\nyou can also check js console"
      )
    )
    
    goto rwl.area.centerX(area) rwl.area.centerY(area)
    square rwl.area.width(area) - 10 rwl.area.height(area) - 10 10 : c#shared.theme.back
    
    if self.loading (
      goto rwl.area.centerX(area) rwl.area.centerY(area)
      direction timer * 720
      icon "sync" .75 : c#shared.theme.text
      return
    )
    
    //log self.rwlInst
    void self.rwlInst.render(area)
  )
  
  def checkResp() (
    if self.resp == null (
      self.loading = false
      return
    )
    
    if self.resp.isFinished (
      if self.resp.isValid (
        self.loading = false
        
        void self.createInsts()
        void self.loadText(self.resp.content)
        
        self.resp = null
      )
    ) else (
      self.loading = true
      void self.resp.update()
    )
  )
  
  def loadAst(object ast) (
    void self.createInsts()
    void self.rwlInst.loadFromAst(ast)
    void self.updateInsts()
  )
  
  def loadText(string text) (
    void self.createWorker()
    self.worker.parseText = text
  )
  
  def loadUrl(object url) (
    self.url @= url
    void self.createInsts()
    self.resp @= net.fetch.url(url)
  )
  
  def getIcon() (
    return self.icon
  )
  def getTitle() (
    return self.title
  )
)

def init() (
  local parser @= rwl.ast.Parser(import("./src/assets/empty.rwl"))
  self.empty @= parser.parse()
)
